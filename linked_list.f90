MODULE linked_list
    IMPLICIT NONE
    PUBLIC :: LIST_APPEND, LIST_PREPEND, LIST_REMOVE_HEAD, LIST_REMOVE_TAIL, LIST_DEALLOCATE

    TYPE :: NODE
        CLASS(*), ALLOCATABLE :: VALUE
        TYPE(NODE), POINTER :: NEXT => NULL(), PREV => NULL()
    END TYPE NODE

    TYPE :: LIST
        TYPE(NODE), POINTER :: HEAD => NULL(), TAIL => NULL()
        INTEGER :: SIZE = 0
    END TYPE LIST

CONTAINS
    SUBROUTINE LIST_APPEND(LI, VALUE)
        TYPE(LIST), INTENT(INOUT) :: LI
        CLASS(*), INTENT(IN) :: VALUE

        TYPE(NODE), POINTER :: ND

        ALLOCATE (ND)
        ALLOCATE (ND%VALUE, SOURCE=VALUE)

        IF (LI%SIZE == 0) THEN
            LI%HEAD => ND
            LI%TAIL => ND
        ELSE
            LI%TAIL%NEXT => ND
            ND%PREV => LI%TAIL
            LI%TAIL => ND
        END IF

        LI%SIZE = LI%SIZE + 1

    END SUBROUTINE LIST_APPEND

    SUBROUTINE LIST_PREPEND(LI, VALUE)
        TYPE(LIST), INTENT(INOUT) :: LI
        CLASS(*), INTENT(IN) :: VALUE

        TYPE(NODE), POINTER :: ND

        ALLOCATE (ND)
        ALLOCATE (ND%VALUE, SOURCE=VALUE)

        IF (LI%SIZE == 0) THEN
            LI%HEAD => ND
            LI%TAIL => ND
        ELSE
            LI%HEAD%PREV => ND
            ND%NEXT => LI%HEAD
            LI%HEAD => ND
        END IF

        LI%SIZE = LI%SIZE + 1

    END SUBROUTINE LIST_PREPEND

    SUBROUTINE LIST_REMOVE_HEAD(LI)
        TYPE(LIST), INTENT(INOUT) :: LI

        TYPE(NODE), POINTER :: TEMP

        TEMP => LI%HEAD
        LI%HEAD => LI%HEAD%NEXT

        IF (ASSOCIATED(LI%HEAD)) THEN
            LI%HEAD%PREV => NULL()
        ELSE
            LI%TAIL => NULL()
        END IF

        LI%SIZE = LI%SIZE - 1

        DEALLOCATE (TEMP%VALUE)
        DEALLOCATE (TEMP)

    END SUBROUTINE LIST_REMOVE_HEAD

    SUBROUTINE LIST_REMOVE_TAIL(LI)
        TYPE(LIST), INTENT(INOUT) :: LI

        TYPE(NODE), POINTER :: TEMP

        TEMP => LI%TAIL
        LI%TAIL => LI%TAIL%PREV

        IF (ASSOCIATED(LI%TAIL)) THEN
            LI%TAIL%NEXT => NULL()
        ELSE
            LI%HEAD => NULL()
        END IF

        LI%SIZE = LI%SIZE - 1

        DEALLOCATE (TEMP%VALUE)
        DEALLOCATE (TEMP)

    END SUBROUTINE LIST_REMOVE_TAIL

    SUBROUTINE LIST_DEALLOCATE(LI)
        TYPE(LIST), INTENT(INOUT) :: LI

        TYPE(NODE), POINTER :: TEMP

        DO WHILE (ASSOCIATED(LI%HEAD))
            TEMP => LI%HEAD
            LI%HEAD => LI%HEAD%NEXT

            DEALLOCATE (TEMP%VALUE)
            DEALLOCATE (TEMP)
        END DO

        LI%TAIL => NULL()
        LI%SIZE = 0

    END SUBROUTINE LIST_DEALLOCATE

END MODULE linked_list
